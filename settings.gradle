pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        exclusiveContent {
            forRepository {
                maven {
                    name 'Fabric'
                    url uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('fabric-loom')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name 'NeoForge'
                    url uri('https://maven.neoforged.net/releases')
                }
            }
            filter {
                // includeGroupAndSubgroups has an issue until Gradle 8.7, see https://github.com/gradle/gradle/issues/26569
                includeGroupByRegex("net\\.neoforged(?:\\..+)?")
                includeGroup('codechicken')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name 'Sponge Snapshots'
                    url uri('https://repo.spongepowered.org/repository/maven-public')
                }
            }
            filter {
                includeGroupByRegex("org\\.spongepowered(?:\\..+)?")
                includeGroup('net.minecraftforge')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name 'ParchmentMC'
                    url 'https://maven.parchmentmc.org'
                }
            }
            filter { includeGroupByRegex("org\\.parchmentmc(?:\\..+)?") }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = "$archives_base_name"
